/*
 * Copyright (C) 2023 Lightstreamer Srl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package com.lightstreamer.client.hxcbridge;

import cpp.Pointer;
import cpp.RawPointer;
import com.lightstreamer.client.SubscriptionListener;

class SubscriptionListenerAdapter implements SubscriptionListener extends cpp.Finalizable {
  final _listener: Pointer<NativeSubscriptionListener>;

  public function new(listener: Pointer<NativeSubscriptionListener>) {
    super();
    _listener = listener;
  }

  override function finalize() {
    _listener.destroy();
  }

  public function onClearSnapshot(itemName: Null<String>, itemPos: Int): Void {
    var itemName = itemName ?? "";
    _listener.ref.onClearSnapshot(itemName, itemPos);
  }
  public function onCommandSecondLevelItemLostUpdates(lostUpdates: Int, key: String): Void {
    _listener.ref.onCommandSecondLevelItemLostUpdates(lostUpdates, key);
  }
  public function onCommandSecondLevelSubscriptionError(code: Int, message: String, key: String): Void {
    _listener.ref.onCommandSecondLevelSubscriptionError(code, message, key);
  }
  public function onEndOfSnapshot(itemName: Null<String>, itemPos: Int): Void {
    var itemName = itemName ?? "";
    _listener.ref.onEndOfSnapshot(itemName, itemPos);
  }
  public function onItemLostUpdates(itemName: Null<String>, itemPos: Int, lostUpdates: Int): Void {
    var itemName = itemName ?? "";
    _listener.ref.onItemLostUpdates(itemName, itemPos, lostUpdates);
  }
  public function onItemUpdate(update: ItemUpdate): Void {
    // hxObj is released by NativeItemUpdate's destructor
    var hxObj: RawPointer<cpp.Void> = HaxeCBridge.retainHaxeObject(new HxCBridgeItemUpdate(update));
    var hxObjPtr = Pointer.fromRaw(hxObj).ptr;
    // ***** WARNING *****
    //
    // var _update = new NativeItemUpdate(hxObjPtr);
    // _listener.ref.onItemUpdate(_update);
    //
    // The c++ code generated by Haxe from the lines above causes the C++ compiler to attempt to invoke
    // the ItemUpdate copy constructor, which however has been deleted.
    // To avoid it, I inline the right code manually
    untyped __cpp__("Lightstreamer::ItemUpdate _update({0})", hxObjPtr);
    untyped __cpp__("{0}.onItemUpdate(_update)", _listener.ref);
  }
  public function onListenEnd(): Void {
    _listener.ref.onListenEnd();
  }
  public function onListenStart(): Void {
    _listener.ref.onListenStart();
  }
  public function onSubscription(): Void {
    _listener.ref.onSubscription();
  }
  public function onSubscriptionError(code: Int, message: String): Void {
    _listener.ref.onSubscriptionError(code, message);
  }
  public function onUnsubscription(): Void {
    _listener.ref.onUnsubscription();
  }
  public function onRealMaxFrequency(frequency: Null<String>): Void {
    var frequency = frequency ?? "";
    _listener.ref.onRealMaxFrequency(frequency);
  }
}